// Apply the java plugin to add support for Java
apply plugin: 'java'

// In this section you declare where to find the dependencies of your project
repositories {
    mavenCentral()
    maven {
        url "https://jitpack.io"
    }
}

// In this section you declare the dependencies for your production and test code
dependencies {
    compile 'com.github.CjS77:burlap:0fab817'
    compile 'com.fasterxml.jackson.core:jackson-core:2.6.0'
    compile 'com.fasterxml.jackson.core:jackson-annotations:2.6.0'
    compile 'com.fasterxml.jackson.core:jackson-databind:2.6.0'
    compile fileTree(dir: 'lib', include: ['*.jar'])
}

task(runVisualise, group: 'execute', description: 'Run an interactive version of the Space Invader game as Player 1',
        dependsOn: 'classes', type: JavaExec) {
    main = 'za.co.nimbus.game.InteractiveGame'
    classpath = sourceSets.main.runtimeClasspath
    args = ["Foxhole", "0"]
}

task(trainAgent, group: 'execute', description: 'Train the Agent with the given learning algorithm',
        dependsOn: 'classes', type: JavaExec) {
    jvmArgs '-Xmx4g'
    main = 'za.co.nimbus.game.SpaceCamp'
    def numEpisodes = project.hasProperty('nEps')? nEps : 100
    def oppStrategy = project.hasProperty('opp')? opp : "SittingDuck"
    def pNum = project.hasProperty('pNum')? pNum : "0"
    def ε = project.hasProperty('epsilon')? epsilon : 0.1
    def α = project.hasProperty('lrate')? lrate : 0.001

    args = [oppStrategy, pNum, numEpisodes, ε, α]
    classpath = sourceSets.main.runtimeClasspath
}

